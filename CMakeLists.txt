cmake_minimum_required(VERSION 2.8)

project(ITK_BENCHMARK)

option(${PROJECT_NAME}_SUPERBUILD "" ON)

if (${PROJECT_NAME}_SUPERBUILD)
  # External Project Build of ITK
  include(ExternalProject)

  set( ITK_SOURCE_DIR ${CMAKE_BINARY_DIR}/ITK-src )
  set( ITK_BUILD_DIR ${CMAKE_BINARY_DIR}/ITK-build CACHE INTERNAL "Path to store itk binaries")
  #set( ITK_INSTALL_DIR ${CMAKE_BINARY_DIR}/ITK-install CACHE INTERNAL "Path to store itk binaries")

  ExternalProject_Add(ITK
    GIT_REPOSITORY "git://itk.org/ITK.git"
    GIT_TAG ef3e18aa90a64c1913fa29f9e31711666ca679de
    UPDATE_COMMAND ""
    DOWNLOAD_DIR ${ITK_SOURCE_DIR}
    SOURCE_DIR ${ITK_SOURCE_DIR}
    BINARY_DIR ${ITK_BUILD_DIR}
    INSTALL_DIR ${ITK_BUILD_DIR}
    LOG_DOWNLOAD 1
    LOG_UPDATE 0
    LOG_CONFIGURE 0
    LOG_BUILD 0
    LOG_TEST 0
    LOG_INSTALL 0
    CMAKE_GENERATOR ${gen}
    CMAKE_ARGS
      -DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER}
      -DCMAKE_CXX_COMPILER_ARG1:STRING=${CMAKE_CXX_COMPILER_ARG1}
      -DCMAKE_C_COMPILER:STRING=${CMAKE_C_COMPILER}
      -DCMAKE_C_COMPILER_ARG1:STRING=${CMAKE_C_COMPILER_ARG1}
      -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
      -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
      -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
      -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}
      -DBUILD_TESTING:BOOL=OFF
      -DBUILD_EXAMPLES:BOOL=OFF
      -DITK_USE_BRAINWEB_DATA:BOOL=ON
    INSTALL_COMMAND ""
    )

  set(ITK_BENCHMARK_EXECUTABLE_OUTPUT_PATH  "${CMAKE_BINARY_DIR}/bin")

  ExternalProject_Add(${PROJECT_NAME}
    UPDATE_COMMAND ""
    DOWNLOAD_COMMAND ""
    SOURCE_DIR ${CMAKE_SOURCE_DIR}
    BINARY_DIR ${CMAKE_BINARY_DIR}#/${PROJECT_NAME}-build
    LOG_DOWNLOAD 1
    LOG_UPDATE 0
    LOG_CONFIGURE 0
    LOG_BUILD 0
    LOG_TEST 0
    LOG_INSTALL 0
    CMAKE_GENERATOR ${gen}
    CMAKE_ARGS
      -DCMAKE_RUNTIME_OUTPUT_DIRECTORY:STRING=${ITK_BENCHMARK_EXECUTABLE_OUTPUT_PATH}
      -DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER}
      -DCMAKE_CXX_COMPILER_ARG1:STRING=${CMAKE_CXX_COMPILER_ARG1}
      -DCMAKE_C_COMPILER:STRING=${CMAKE_C_COMPILER}
      -DCMAKE_C_COMPILER_ARG1:STRING=${CMAKE_C_COMPILER_ARG1}
      -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
      -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
      -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
      -DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}
      -DITK_DIR:PATH=${ITK_BUILD_DIR}
      -D${PROJECT_NAME}_SUPERBUILD:BOOL=OFF
    INSTALL_COMMAND ""
    DEPENDS ITK
    )
    return()
endif()

# Find ITK Library
find_package(ITK REQUIRED)
include(${ITK_USE_FILE})

#Enable CTest
enable_testing()

# Add sub-directory
add_subdirectory(Benchmarks)
